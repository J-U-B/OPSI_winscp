; setup.ins WinSCP
; Installer-Typ: Inno Setup with custom inf file
; Switches can be found under http://www.jrsoftware.org/ishelp/index.php?topic=setupcmdline
;======================================================================
[Initial]
;======================================================================
; Borrmann, 2014-08-20
setLogLevel=6
; Log Errors in Logfile but don't abort:
ExitOnError=false 
; Show syntax errors in the script:
ScriptErrorMessages=on
; Dont trace step by step through the script:
TraceMode=off 
; Let started programs run in front of the winst window
StayOnTop=false 

;======================================================================
[Actions]
;======================================================================
include_insert "%ScriptPath%\variables.ins"
include_append "%ScriptPath%\exitcode_inno.ins"

requiredWinstVersion >= "4.11.3"

Set $ProductId$       = "winscp"
Set $ProductName$     = "WinSCP"
Set $Version$         = "5.5.4"
Set $MinimumSpace$    = "5 MB"
Set $InstallDir$      = "%ProgramFiles32Dir%\"+$ProductId$

Set $LogDir$          = "%SystemDrive%\tmp"
Set $InstallationLog$ = $LogDir$ + "\"+$ProductId$+".log"
Set $UninstallLog$    = $LogDir$ + "\"+$ProductId$+"_uninstall.log"

if not(HasMinimumSpace ("%SYSTEMDRIVE%", $MinimumSpace$))
	LogError "Not enough space on %SYSTEMDRIVE%: . "+$MinimumSpace$+" on Drive %SYSTEMDRIVE%: needed for "+$ProductId$+"."
	isFatalError
else
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $ProductName$

	if FileExists("%ScriptPath%\delsub.ins")
		comment  "Starting uninstall sub section"
		sub "%ScriptPath%\delsub.ins"	
	endif
	
	Message=Installing WinSCP
	Winbatch_Install
	sub_check_exitcode
endif

[Winbatch_Install]
"%ScriptPath%\files\winscp554setup.exe" /VERYSILENT /NOCANCEL /LOG="$InstallationLog$" /DIR="$InstallDir$" /NORESTART /LOADINF=%ScriptPath%\winscp.inf