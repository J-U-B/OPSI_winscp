;===============================================================================
; checkinstance.opsiinc
; J. Boettge <boettge@mpi-halle.mpg.de> 2019-03-20 12:29:09 +0100
;===============================================================================

set $PSDETAIL_opr$ = "-e"
set $PSDETAIL_mod$ = ""

;===============================================================================
[sub_checkinstance]
;===============================================================================
comment "Checking for running instances and kill it (if allowed)."

if FileExists($PSDETAIL$)
	switch $PSDETAIL_mod$
		case "-c"
			;=== retrieve details for process which commandline matches the argument
			set $PSDETAIL_opr$ = "-c"
		endCase
		case "-p"
			;=== retrieve details for process with this process id
			set $PSDETAIL_opr$ = "-p"
		endCase
		case "-n"
			;=== retrieve details for process with this name
			set $PSDETAIL_opr$ = "-n"
		endCase	
		defaultCase
			;=== retrieve details for process with this executable (full path)
			set $PSDETAIL_opr$ = "-e"
		endCase					
	endSwitch

	winbatch_psdetail_count
	set $Proc_Cnt$ = getLastExitCode
	if $Proc_Cnt$ > "0"
		if $Kill_Running$ = "True"
			comment $Proc_Cnt$ + " running instance(s) of " + $MainBin$ + " found, killing them."
			winbatch_psdetail_kill
			set $Proc_Killed$ = getLastExitCode
			comment $Proc_Killed$ + " instance(s) of " + $MainBin$ + " were killed."
			if not ($Proc_Cnt$=$Proc_Killed$)
				logWarning "Number of killed instances (" + $Proc_Killed$ + ") does not match expectations (" + $Proc_Cnt$ + ")."
			endif
		else
			logError $Proc_Cnt$+" running instance(s) of " + $MainBin$ + " found, but I'm not allowed to kill them."
			isFatalError "running instance"
		endif
	else
		if $Proc_Cnt$ = "0"
			comment "no running instance of " + $MainBin$ + " found"
		else
			logError "psdetail returns " + $Proc_Cnt$ + " (unexpected)"
		endif
	endif
else
	logError "can't find psdetail.exe"
endif

;===============================================================================
[winbatch_psdetail_count]
;===============================================================================
"$PSDETAIL$" $PSDETAIL_opr$ "$MainBin$"

;===============================================================================
[winbatch_psdetail_kill]
;===============================================================================
"$PSDETAIL$" -k $PSDETAIL_opr$ "$MainBin$"
